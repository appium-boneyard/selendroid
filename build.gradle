buildscript {
    ext.bintrayUser = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    ext.bintrayKey = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    ext.bintrayEnabled = project.bintrayUser && project.bintrayKey

    if (bintrayEnabled) {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply from: 'properties.gradle'
group = selGroup
version = selVersion

allprojects {
    repositories {
        jcenter()
    }

    version = '0.18.0-SNAPSHOT'

    ext {
        // https://bintray.com/android/android-tools/com.android.tools.build.gradle/view
        androidBuildToolsGradle = 'com.android.tools.build:gradle:1.4.0-beta6'
        // https://bintray.com/bintray/jcenter/com.jakewharton.sdkmanager:gradle-plugin
        sdkManager = 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        // https://bintray.com/bintray/jcenter/org.json%3Ajson/20090211/view
        json = 'org.json:json:20090211'
        seleniumVersion = '2.48.2'
        compileSdkVersion = 23
        buildToolsVersion = '23.0.0'
        minSdkVersion = 10
        targetSdkVersion = 16
        sourceCompatibilityVersion = 1.7
        targetCompatibilityVersion = 1.7
        junit = 'junit:junit:4.12'
    }
}

if (bintrayEnabled) {
    apply from: 'jcenter.gradle'
    allprojects {
        apply plugin: 'com.jfrog.bintray'
    }
// https://github.com/bintray/bintray-examples/blob/master/gradle-multi-example/build.gradle
    subprojects {
        apply plugin: 'maven'

        if (project.plugins.hasPlugin('java')) {
            task sourceJar(type: Jar) {
                classifier = 'sources'
                from sourceSets.main.allSource
            }
            task javadocJar(type: Jar, dependsOn: javadoc) {
                classifier = 'javadoc'
                from javadoc.destinationDir
            }

            // Add the sourceJars to non-extractor modules
            artifacts {
                archives sourceJar
                archives javadocJar
            }
        }
    }
}
